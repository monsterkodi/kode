###
 0000000   0000000   000   000  00000000    0000000  00000000  00     00   0000000   00000000   
000       000   000  000   000  000   000  000       000       000   000  000   000  000   000  
0000000   000   000  000   000  0000000    000       0000000   000000000  000000000  00000000   
     000  000   000  000   000  000   000  000       000       000 0 000  000   000  000        
0000000    0000000    0000000   000   000   0000000  00000000  000   000  000   000  000        
###

kstr = require 'kstr'
slash = require 'kslash'
print = require './print'

# maps locations in a single generated js file back to locations in the original source file

class SourceMap
    
    @: (@source) -> 
        
        @jsline = 0
        @lines  = []
        @cache  = []

    #  0000000   0000000   00     00  00     00  000  000000000  
    # 000       000   000  000   000  000   000  000     000     
    # 000       000   000  000000000  000000000  000     000     
    # 000       000   000  000 0 000  000 0 000  000     000     
    #  0000000   0000000   000   000  000   000  000     000     
    
    commit: (s, tl) ->
        
        if tl == true
            
            while s[0] == '\n'
                s = s[1..]
                @jsline++
                            
            log b7('c'), g4(lpad(4 @jsline)), s
            
            @jsline += @solve s
            
            @cache  = []
            
        else if tl?.type
            
            log b4('t'), r2(lpad(4 tl.line ? '?')), s
            @cache.push [s, tl]
            
        else
            
            log b4('.'), b2(lpad(4 @jsline)), w2(s)
            
    #  0000000   0000000   000      000   000  00000000  
    # 000       000   000  000      000   000  000       
    # 0000000   000   000  000       000 000   0000000   
    #      000  000   000  000         000     000       
    # 0000000    0000000   0000000      0      00000000  
    
    solve: (s) ->

        return 0 if empty s
        
        p = 0
        slcs = []
        
        jsidx = 0
        jslns = s.split '\n'
        
        ln = jslns[jsidx]
        
        log y5('solve'), @jsline
        
        for ci in 0...@cache.length
            
            [cs, tok] = @cache[ci]
            
            while (i = ln.indexOf cs, p) <= 0
                break if jsidx >= jslns.length-1
                ln = jslns[++jsidx]
                p = 0
            
            if i >= 0 and jsidx < jslns.length
                ji = @jsline+jsidx
                slcs.push [ln[i...i+cs.length], [ji, tok.line, tok.col]]
                @add [tok.line-1, tok.col], [ji, i], ln[i...i+cs.length]
                log b6(ji), w3(i), r4(tok.line), r2(tok.col), cs+r2 '◂'
                p = i+cs.length    
            else
                log "srcmap.solve can't locate tok #{tok.text} in #{s}"
                
        log @jsline, s, slcs
        jslns.length-1
        
    # 0000000     0000000   000   000  00000000  
    # 000   000  000   000  0000  000  000       
    # 000   000  000   000  000 0 000  0000000   
    # 000   000  000   000  000  0000  000       
    # 0000000     0000000   000   000  00000000  
    
    done: (s) ->
        
        ls = s.split '\n'
        
        log b5('d'), @jsline, ls.length, @lines.length
        
        log ls
                
        for li in 0...ls.length
            
            ln = ls[li]
            
            log "#{b3 kstr.lstrip li, 4} #{ln}#{r2 '◂'}" 
            
            if lm = @lines[li]
                for c in lm.columns
                    continue if not c
                    log "#{red c.jsstr} #{c.sourceLine} #{c.sourceColumn}"
            
    #  0000000   0000000    0000000    
    # 000   000  000   000  000   000  
    # 000000000  000   000  000   000  
    # 000   000  000   000  000   000  
    # 000   000  0000000    0000000    
    
    add: (source, target, jsstr) -> # source and target: [line, column]
        
        [line, column] = target
        @lines[line] ?= new LineMap line
        @lines[line].add column, source, jsstr

    # sourceLocation: (srcloc) ->
    #         
        # [line, column] = srcloc
        # line-- while not ((lineMap = @lines[line]) or (line <= 0))
        # lineMap and lineMap.sourceLocation column

    #  0000000   00000000  000   000  00000000  00000000    0000000   000000000  00000000  
    # 000        000       0000  000  000       000   000  000   000     000     000       
    # 000  0000  0000000   000 0 000  0000000   0000000    000000000     000     0000000   
    # 000   000  000       000  0000  000       000   000  000   000     000     000       
    #  0000000   00000000  000   000  00000000  000   000  000   000     000     00000000  
    
    generate: (code) ->
        
        writingline      = 0
        lastColumn       = 0
        lastSourceLine   = 0
        lastSourceColumn = 0
        needComma        = no
        buffer           = ""

        for lineMap, lineNumber in @lines 
            continue if not lineMap
            for mapping in lineMap.columns 
                continue if not mapping
                while writingline < mapping.line
                    lastColumn = 0
                    needComma = no
                    buffer += ";"
                    writingline++

                # Write a comma if we've already written a segment on this line.

                if needComma
                    buffer += ","
                    needComma = no

                # Write the next segment. Segments can be 1, 4, or 5 values.    
                # If just one, then it is a generated column which doesn't match anything in the source code.
                
                # The starting column in the generated source, relative to any previous recorded
                # column for the current line:

                buffer += @encodeVlq mapping.column - lastColumn
                lastColumn = mapping.column

                # The index into the list of sources:

                buffer += @encodeVlq 0

                # The starting line in the original source, relative to the previous source line.

                buffer += @encodeVlq mapping.sourceLine - lastSourceLine
                lastSourceLine = mapping.sourceLine

                # The starting column in the original source, relative to the previous column.

                buffer += @encodeVlq mapping.sourceColumn - lastSourceColumn
                lastSourceColumn = mapping.sourceColumn
                needComma = yes

        file = slash.file slash.swapExt(@source, 'js'), @source
                
        version:        3
        file:           file
        # sourceRoot:     ''
        sources:        [slash.file(@source) or '']
        # names:          []
        mappings:       buffer
        # sourcesContent: [code]

    #       000   0000000   0000000   0000000   0000000    00000000  
    #       000  000       000       000   000  000   000  000       
    #       000  0000000   000       000   000  000   000  0000000   
    # 000   000       000  000       000   000  000   000  000       
    #  0000000   0000000    0000000   0000000   0000000    00000000  
    
    jscode: (v3Map) ->
        
        encoded = @base64encode JSON.stringify v3Map
        dataURL = "//# sourceMappingURL=data:application/json;base64,#{encoded}"
        sourceURL = "//# sourceURL=#{@source}"
        "\n\n#{dataURL}\n#{sourceURL}\n"
        
        
    decodejs: (encoded) ->
        
        JSON.parse @base64decode encoded
        
    # 00000000  000   000   0000000   0000000   0000000    00000000  
    # 000       0000  000  000       000   000  000   000  000       
    # 0000000   000 0 000  000       000   000  000   000  0000000   
    # 000       000  0000  000       000   000  000   000  000       
    # 00000000  000   000   0000000   0000000   0000000    00000000  
    
    # Note that SourceMap VLQ encoding is "backwards".  MIDI-style VLQ encoding puts
    # the most-significant-bit (MSB) from the original value into the MSB of the VLQ
    # encoded value (see [Wikipedia](https://en.wikipedia.org/wiki/File:Uintvar_coding.svg)).
    # SourceMap VLQ does things the other way around, with the least significat four
    # bits of the original value encoded into the first byte of the VLQ encoded value.
    
    encodeVlq: (value) ->
        
        VLQ_SHIFT            = 5
        VLQ_CONTINUATION_BIT = 1 << VLQ_SHIFT            # 0010 0000
        VLQ_VALUE_MASK       = VLQ_CONTINUATION_BIT - 1  # 0001 1111
        
        signBit = if value < 0 then 1 else 0 # Least significant bit represents the sign.

        valueToEncode = (Math.abs(value) << 1) + signBit # The next bits are the actual value.

        # Make sure we encode at least one character, even if valueToEncode is 0.
        answer = ''
        while valueToEncode or not answer
            nextChunk = valueToEncode & VLQ_VALUE_MASK
            valueToEncode = valueToEncode >> VLQ_SHIFT
            nextChunk |= VLQ_CONTINUATION_BIT if valueToEncode
            answer += @encodeBase64 nextChunk
        answer

    base64decode: (src) -> Buffer.from(src, 'base64').toString()
        
    base64encode: (src) ->
        
        # if typeof Buffer is 'function'
        Buffer.from(src).toString('base64')
        # else if typeof btoa is 'function'
            # # The contents of a `<script>` block are encoded via UTF-16, so if any extended
            # # characters are used in the block, btoa will fail as it maxes out at UTF-8.
            # # See https://developer.mozilla.org/en-US/docs/Web/API/WindowBase64/Base64_encoding_and_decoding#The_Unicode_Problem
            # # for the gory details, and for the solution implemented here.
            # btoa encodeURIComponent(src).replace /%([0-9A-F]{2})/g, (match, p1) ->
                # String.fromCharCode '0x' + p1
        # else
            # throw new Error 'Unable to base64 encode inline sourcemap.'
            
        
    encodeBase64: (value) -> 
    
        BASE64_CHARS = 'ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/'
        BASE64_CHARS[value]

# 000      000  000   000  00000000  00     00   0000000   00000000   
# 000      000  0000  000  000       000   000  000   000  000   000  
# 000      000  000 0 000  0000000   000000000  000000000  00000000   
# 000      000  000  0000  000       000 0 000  000   000  000        
# 0000000  000  000   000  00000000  000   000  000   000  000        

# keeps track of source positions for a single line of js code

class LineMap
    
    @: (@line) -> @columns = []

    add: (column, srcloc, jsstr) ->
        [sourceLine, sourceColumn] = srcloc
        if @columns[column] 
            log "LineMap has column #{column}" sourceLine, sourceColumn, options
            return
        @columns[column] = {line: @line, column, sourceLine, sourceColumn, jsstr}

    # sourceLocation: (column) ->
#         
        # column-- while not ((mapping = @columns[column]) or (column <= 0))
        # mapping and [mapping.sourceLine, mapping.sourceColumn]
        
module.exports = SourceMap



